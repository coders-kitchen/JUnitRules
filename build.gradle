apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

sourceCompatibility = "1.6"


repositories {
	mavenCentral()
	maven {
		url 'http://download.eclipse.org/rt/eclipselink/maven.repo/'
	}
}


dependencies {
	compile 'junit:junit-dep:4.10', 'org.eclipse.persistence:javax.persistence:2.0.3',
			'javax:javaee-api:6.0'
	testCompile 'org.mockito:mockito-core:1.9.0', 'org.hamcrest:hamcrest-core:1.3'
	testRuntime 'org.eclipse.persistence:eclipselink:2.3.0',
			'org.eclipse.persistence:org.eclipse.persistence.jpa.modelgen.processor:2.3.0',
			'org.apache.derby:derby:10.9.1.0'
}

task sourcesJar(type: Jar) {
	from sourceSets.main.allSource
	classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from 'build/docs/javadoc'
}

artifacts {
	archives jar
	archives javadocJar
	archives sourcesJar
}

if (hasProperty('sonatypeUsername')) {
	signing {
		sign configurations.archives
	}

	uploadArchives {
		repositories {
			mavenDeployer {
				beforeDeployment { MavenDeployment deployment -> signPom(deployment) }

				repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
					authentication(userName: sonatypeUsername, password: sonatypePassword)
				}
				pom.project {
					name 'JUnit Rules'
					packaging 'jar'
					description 'This archive represents a set of JUnit Rules that aren\'t available in the basic setup.'
					url 'http://coders-kitchen.github.com'

					scm {
						url 'scm:git@github:CodersKitchen/JUnitRules.git'
						connection 'scm:git@github:CodersKitchen/JUnitRules.git'
						developerConnection 'scm:git@github:CodersKitchen/JUnitRules.git'
					}

					licenses {
						license {
							name 'The Apache Software License, Version 2.0'
							url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
							distribution 'repo'
						}
					}


					developers {
						developer {
							id 'peterdaum'
							name 'Peter Daum'
						}
					}
				}
				pom.whenConfigured { pom ->
					pom.dependencies.findAll { dep -> dep.artifactId != 'junit' }.each{pom.dependencies.remove(it)}
				}
			}
		}
	}
}

